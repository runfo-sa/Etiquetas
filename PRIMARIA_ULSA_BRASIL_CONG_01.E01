^XA
^FX Etiqueta PRIMARIA_BRASIL_CONG_02.E01
^FX Se desglosa Faenador y Elaborador. Se agrega "Sacrificado en" y "Despiece en"

^FX Start Metadata#Labelary
^FX ProductId: 1457
^FX End Metadata

^CI28
^MTT ^FX Trasferencia termica
~JSN ^FX Backfeed normal
^JZN ^FX No reimprimir!!
^LT40
^LH20,0
^PW1200

^CW1,E:ArialNB.TTF 
^CW2,E:ArialN.TTF 

^FT20,830^A0B,18^FDPBC03^FS

^FT240,30^A0N,25^FH%^FDEstab. Faenador / Sacrificado en: Estab. N%C2%BA Of. 3203 - Runfo S.A.^FS
^FT240,55^A0N,25^FH%^FDEstab. Elaborador / Despiece en: Estab. N%C2%BA Of. 3203 - Runfo S.A.^FS
^FT240,80^A0N,25^FH%^FDAv R%C3%ADo de La Plata 7650 e/Dungenes y Dupuy, Virrey del Pino, Buenos Aires, Argentina^FS

^FT240,107^A0N,29^FH%^FDDados do importador:^FS
^FT260,107^A1N,29^FH%^FD^FS
^FT240,133^A0N,29^FH%^FDEndere%C3%A7o:^FS
^FT130,133^A1N,29,24^FB970,1,0,L^FH%^FD^FS
^FT240,161^A0N,29^FH%^FDCEP:^FS
^FT60,161^A1N,29,28^FB740,1,0,L^FH%^FD^FS
^FT800,161^A0N,29^FH%^FDCNPJ: ^FS
^FT880,161^A1N,29,28^FB220,1,0,L^FH%^FD^FS



^FT0,190^A0N,30,40^FB1121,1,0,C^FH%^FDRegistro no Minist%C3%A9rio da Agricultura SIF/DIPOA sob n%C2%B0 0017/3203^FS
^FT0,218^A0N,28,25^FB1121,1,0,C^FH%^FDMANTER CONGELADA A -18%C2%BAC. N%C3%83O RECONGELAR / MANTENER CONGELADA A -18%C2%BAC. NO RECONGELAR^FS

^FT0,252^A0N,35^FH\^FDCARNE CONGELADA DE BOVINO SEM OSSO^FS
^FT0,283^A0N,35^FH\^FDCARNE VACUNA CONGELADA SIN HUESO^FS


^FX RECUADRO PRODUCTO
^FO0,288^GB880,110,6,,0^FS
^CI10
^FT8,340^A0N,46,53^FB880,1,0,L^FH%^FD[@MercaderiasTraducciones.sDescripcion6@]^FS
^FT8,385^A0N,46,55^FB880,1,0,L^FH%^FD[@MercaderiasTraducciones.sDescripcion5@]^FS
^CI28
^FT945,407^A0B,23,21^FB120,1,0,R^FH\^FD[@Primarias.iPeso@]^FS
^FT945,395^A0B,23,21^FB120,1,0,L^FH\^FD[@PlanesDetalleSalida.PlanId@]^FS
^FT920,400^A0B,23,21^FB120,1,0,C^FH\^FD[@Mercaderias.sCodigo@]^FS

^FX Conservacion
^FT0,425^A0N,30,18^FB1200,1,0,L^FH%^FDCONSERVA%C3%87%C3%83O DOM%C3%89STICA: A -18%C2%BAC (FREEZER): VIDE VALIDADE / A -4%C2%BAC (CONGELADOR): 90 DIAS / A 4%C2%BAC (GELADERA): 3 DIAS^FS

^FX Datamatrix
^FO960,250^BXN,10,200,0,0,1,~,1
^FD[@Primarias.sCodbar@]^FS
^FT965,420^A0N,20^FD[@Primarias.sCodbar@]^FS ^FX Codigo legible humanos

^FX DATOS SENASA Y ADICIONALES
^FT78,470^A0N,42,40^FB450,1,0,L^FH%^FDSENASA N%C2%B0 [@Mercaderias.sCodSenasa@]^FS
^FT78,510^A0N,42,40^FDINDUSTRIA ARGENTINA^FS

^FT0,545^A0N,29,27^FB550,1,0,C^FH%^FDN%C3%83O CONT%C3%89M GL%C3%9ATEN / NO CONTIENE GLUTEN^FS
^FT0,575^A0N,29,27^FB550,1,0,C^FH%^FDEMBALADO %C3%81 V%C3%81CUO / ENVASADO AL VACIO^FS
^FT0,605^A0N,29,28^FB550,1,0,C^FH%^FDPESO DA EMBALAGEM: 10 g / VENTA AL PESO^FS
^FT0,635^A0N,29^FB550,1,0,C^FH%^FDORIGEM ARGENTINA / ORIGEN ARGENTINA^FS

^FT0,667^A0N,25,24^FB270,1,0,R^FH%^FDDATA DE PRODU%C3%87%C3%83O^FS
^FT0,692^A0N,25,24^FB270,1,0,R^FH%^FDFECHA DE ELABORACI%C3%93N^FS
^FT295,686^A0N,42,42^FB250,1,0,L^FD[@identificadores.dFechaProduccion;FFdd/MM/yyyy@]^FS

^FT0,723^A0N,25,24^FB270,1,0,R^FH%^FDDATA DE CONGELA%C3%87%C3%83O^FS
^FT0,748^A0N,25,24^FB270,1,0,R^FDFECHA DE CONGELACION^FS
^FT295,742^A0N,42,42^FB250,1,0,L^FD[@identificadores.dFechaProduccion;FFdd/MM/yyyy@]^FS

^FT0,779^A0N,25,24^FB270,1,0,R^FDVALIDO ATE^FS
^FT0,804^A0N,25,24^FB270,1,0,R^FDFECHA DE VENCIMIENTO^FS
^FT295,798^A0N,42,42^FB250,1,0,L^FD[@FechaVencimiento.dFecha;FFdd/MM/yyyy@]^FS

^FT0,837^A0N,25,24^FB270,1,0,R^FH%^FDN%C2%B0 DO LOTE/TROPA^FS
^FT0,860^A0N,25,24^FB270,1,0,R^FH%^FDLOTE N%C2%B0/TROPA^FS
^FT295,852^A0N,42,33^FB250,1,0,l^FH%^FD[@identificadores.dFechaProduccion;FFyyyyMMdd@]/[@PrimariaDatosFrigo.iTropa@]^FS


^FX Tabla Nutricional

^FO550,430^GB530,430,4,,0^FS ^FX recuadro
^FO800,510^GB0,306,2,,0^FS ^FX Linea vertical 1
^FO890,510^GB0,306,2,,0^FS ^FX Linea vertical 2
^FO980,510^GB0,306,2,,0^FS ^FX Linea vertical 3

^FT550,465^A1N,28^FB550,1,0,C^FH%^FDINFORMA%C3%87%C3%83O NUTRICIONAL^FS
^FO560,473^GB510,0,4,,0^FS ^FX linea horizontal 1

^FT550,500^A2N,25^FB550,1,0,C^FH%^FDPor%C3%A7%C3%A3o de 100g (1 bife):^FS
^FO560,510^GB510,0,8,,0^FS ^FX linea horizontal 2 - gruesa

^FT800,539^A2N,25^FB90,1,0,C^FH%^FD100 g^FS
^FT890,539^A2N,25^FB90,1,0,C^FH%^FD100 g^FS
^FT980,539^A2N,25^FB100,1,0,C^FH%^FD%25VD*^FS
^FO560,545^GB510,0,2,,0^fs ^FX Linea horizontal 3

^FT560,569^A2N,25^FH%^FDValor energ%C3%A9tico (kcal)^FS
^FT800,569^A2N,25^FB90,1,0,C^FH%^FD220^FS
^FT890,569^A2N,25^FB90,1,0,C^FH%^FD220^FS
^FT980,569^A2N,25^FB100,1,0,C^FH%^FD11^FS
^FO560,576^GB510,0,2,,0^fs ^FX Linea horizontal 4

^FT560,599^A2N,25^FH%^FDCarboidratos (g)^FS
^FT800,599^A2N,25^FB90,1,0,C^FH%^FD0^FS
^FT890,599^A2N,25^FB90,1,0,C^FH%^FD0^FS
^FT980,599^A2N,25^FB100,1,0,C^FH%^FD0^FS
^FO560,606^GB510,0,2,,0^fs ^FX Linea horizontal 5

^FT560,629^A2N,25^FH%^FDProte%C3%ADnas (g)^FS
^FT800,629^A2N,25^FB90,1,0,C^FH%^FD19^FS
^FT890,629^A2N,25^FB90,1,0,C^FH%^FD19^FS
^FT980,629^A2N,25^FB100,1,0,C^FH%^FD25^FS
^FO560,636^GB510,0,2,,0^fs ^FX Linea horizontal 6

^FT560,659^A2N,25^FH%^FDGorduras totais (g)^FS
^FT800,659^A2N,25^FB90,1,0,C^FH%^FD16^FS
^FT890,659^A2N,25^FB90,1,0,C^FH%^FD16^FS
^FT980,659^A2N,25^FB100,1,0,C^FH%^FD29^FS
^FO560,666^GB510,0,2,,0^fs ^FX Linea horizontal 7

^FT560,689^A2N,25^FH%^FDGorduras saturadas (g)^FS
^FT800,689^A2N,25^FB90,1,0,C^FH%^FD2^FS
^FT890,689^A2N,25^FB90,1,0,C^FH%^FD2^FS
^FT980,689^A2N,25^FB100,1,0,C^FH%^FD9^FS
^FO560,696^GB510,0,2,,0^fs ^FX Linea horizontal 8

^FT560,719^A2N,25^FH%^FDGorduras trans (g)^FS
^FT800,719^A2N,25^FB90,1,0,C^FH%^FD0^FS
^FT890,719^A2N,25^FB90,1,0,C^FH%^FD0^FS
^FT980,719^A2N,25^FB100,1,0,C^FH%^FD^FS
^FO560,726^GB510,0,2,,0^fs ^FX Linea horizontal 9

^FT560,749^A2N,25^FH%^FDFibra alimentar (g)^FS
^FT800,749^A2N,25^FB90,1,0,C^FH%^FD0^FS
^FT890,749^A2N,25^FB90,1,0,C^FH%^FD0^FS
^FT980,749^A2N,25^FB100,1,0,C^FH%^FD0^FS
^FO560,756^GB510,0,2,,0^fs ^FX Linea horizontal 10

^FT560,779^A2N,25^FH%^FDS%C3%B3dio (mg)^FS
^FT800,779^A2N,25^FB90,1,0,C^FH%^FD53^FS
^FT890,779^A2N,25^FB90,1,0,C^FH%^FD53^FS
^FT980,779^A2N,25^FB100,1,0,C^FH%^FD2,2^FS
^FO560,786^GB510,0,2,,0^FS ^FX Linea horizontal 11

^FT560,809^A2N,25^FH%^FDFerro (mg)^FS
^FT800,809^A2N,25^FB90,1,0,C^FH%^FD2,9^FS
^FT890,809^A2N,25^FB90,1,0,C^FH%^FD2,9^FS
^FT980,809^A2N,25^FB100,1,0,C^FH%^FD21^FS
^FO560,816^GB510,0,5,,0^FS ^FX Linea horizontal 12 - media

^FT560,845^A2N,25^FB530,1,0,J^FH%^FD*Percentual de valores di%C3%A1rios fornecidos pela por%C3%A7%C3%A3o.^FS


^FS

^FX Ahora las imagenes

^FX Logo ULSA
^FO0,0^GFA,03584,03584,00028,:Z64:
eJztlr1u2zAQx0Vw4FKAa4YgfIWMGQxz7NhX6ZhBsGgEqJYCXrv5TVoGGTJ27VYGHTqWQRcGFXQ9fsl0RBkNunToDbbMH03e/8i7U9P8t2idXkQCYJEBgF1ADNmwvCSAWvDENgRMFRH/H1nfkA35Y27c/4WMVSZ12LTqTBed1TUWV+M1R4kLX7TmaBpMU46NpcVqInhyYlNhQsVvWWHy2ZzS1gQPoRumtUtrE2MV5hKjpsKabvTxJnPxOCScX7DGTGLNPDD0FNN+M8/aGWMTW82Zymw9Y/wUazK7OMHEjImJ8Rm7+DOmagzTqMrWE2MztvoL9gu+L7JHuNdN5cJ4ZqF/MWurzLsXCgt30KtYZnKhOcXYCXbwxcFOLfk5zBnHBAFkbIS93+MrJlMuM2JE1saNwv7IciEFqbsd7k0B/HQ2Sg3CBETwodv50ehdZDFj+IiFk3nWgc8FNkgAnrRbimNjiIJ3kRv8TRJ7eAB3vWlSBW343R1YmxixUrtwYyOjptva80LDgQlZ6gPcaX9gn0Y/VOgLw/jxFpc56MP1ACZ23cgRNQhXaAjuR1ZqsOSIdchsZsR4DSYy12yonjQcM9uMyFRmcU0b4oJVCco1oy+BaYYTC1/iGT262Mgs/VmcUdRu28TYg2d7M8WTg9pkpjGeU8xCrH3iBsZVEWtGrFCOH2ZSI0nWsEV91yJ3UwlB36rUF4IWLs0zfQOy5Bk+sElfOiNwSS2EM0q5AsLrk3kmKc7Wb9WBFak3gu8T0w486KPRUWziorhLIY9y24fQJ/izds29M7Tew8Mwr7XUcHqo/JjFOhHznxXPx6y0sobU2cG6KeBz44uvRH9lGH+p8CaltOVDJzH4Enq78aEGI0GlZsacFLDFQtK7DXZ+YuyVzI2OmnPW65Ztt+4MizzV15frzIh5ldjgGdMtsu6YKTIGplaX7XP2nnz0jKsVvt/8KJjybJ+Zaz6riX1uWjaQ/YfA1pigBRsD2yVGLfGNIjFo/Jr9l8QMgQPbJSbCfkzTkimv4cZJpt6oFde0N8f6bgN7jWx7QzM7T2yLLynqXreC9JnZM3ZjMGa3Ci+0usM8pe/y24+7Et+e8BzujcOUvzVWwr5P9xSsfHzC87u3WARB+/Pb9+nmxBfWyaR6yUX59+1Ujp3KzVNs0X4Doy8png==:914D

^FX Logo Argentina 3203
^FT820,308^GFA,01536,01536,00016,:Z64:
eJzt0jFqwzAUBuCnCqLNXjuIuNALtJsKpr5KoBdIKBSXQmsw1FtygZKTdLAxOFt7gQ4KGroFhwzVYJIKS7albu3UIW/yJ1vv/bYMcKw/1NQRqmtum9RS2AsEpGgG+hk0/HFwlCrfnfWOsdxwfjEMw7XyMHJyk6xyy0Io5zzvvUvKIl93CerJrCmLoqhNWDm9bBRLcx838RUHlGbGhEbqSZTORaLT0oC3y8Zk6SetP83+g15nb6b/l2508WHmr3Mzt8uD5RPhTe+YyMNiu0cmH4SjcDW/95Bug/NTCMkL8yAf63EcWOvo0MZRjpWvO2cc14SyBgK/fb4CLD3K9oDb/WijJowp8wG9tgPUwdCQMtIFDJXj1joQhVEovdkCTCAPVN73reqn32ukjvuQrRvA+jucdMfQX/y20Cr1hXQciL3tkp4vXeO5beG6EoHrW7+091cPxP4/URWhnXQMu8R28NNMOPYjx88kKC0f6//UN4QJpls=:842C

^FX Logo Senasa
^FO0,410^GFA,01536,01536,00012,:Z64:
eJztk7ENwjAQRR2liESTETKKl4A6o4RxEEvEo3gECooUUY7EBt0riHQSpoJfPb3iZP07O/fPF3Pacsw8ypolYScpiX3m84qVKNfgBtyCe7AHD2BRrsA1uDH4FtwZ/B578FDaa7Op5/7Jc8GFHq45F8wX1C/us/e/nWPwi8HP8JPB3+AjfIQP8GHnDJ36bfzLT/ARPqjnb7mj/4IL/fE8AIlaCSw=:F0F0

^PQ1,0,1,Y
^XZ